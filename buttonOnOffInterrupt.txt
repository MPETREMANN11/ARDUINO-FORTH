\ *********************************************************************
\ Manage button by interrupt for On/Off - work in progress
\    Filename:      buttonOnOffInterrupt.txt
\    Date:          23/05/2020
\    Updated:       29/05/2020
\    File Version:  1.0
\    MCU:           ARDUINO all models
\    Copyright:     Marc PETREMANN
\    Author:        Marc PETREMANN
\    GNU General Public License
\ *********************************************************************

\ require pinsDefinition.txt

\ PORT B
37 constant PORTB	\ Port B Data Register
\ PORT D
43 constant PORTD	\ Port D Data Register

eeprom
\ PB5 on Arduino NANO
PORTB %00100000 defPIN: LED
\ PB7 on Arduino MEGA
\ PORTB %10000000 defPIN: LED
\ PD2 on Arduino NANO
PORTD %00000100 defPIN: BUTTON
\ PD0 on Arduino MEGA
\ PORTD %00000001 defPIN: BUTTON
flash

: init.ports ( ---)
    LED output    \ set PB5 for output
  ;

: LED.on ( ---)
    LED high ;
: LED.off ( ---)
    LED low ;
: LED.toggle ( ---)
    LED pin@
    if      LED.off
    else    LED.on
    then ;

: button? ( --- fl)
    BUTTON pin@    \ test if button is pressed
    \ true  = button pressed
    \ false = button released
  ;

\ only for test
\ : button.action ( ---)
\     BUTTON?
\     if
\         LED.toggle
\     then
\   ;

\ EXTERNAL_INTERRUPT
$69 constant EICRA	\ External Interrupt Control Register A
\ $3c constant EIFR	\ External Interrupt Flag Register
$3d constant EIMSK	\ External Interrupt Mask Register

eeprom
$0002 constant INT0Addr \ interrupt addr for INT0
flash

: init.interrupt ( ---)
    %00000011 EICRA mset    \ interrupt on rising edge
    %00000001 EIMSK mset    \ activate INT0
  ;


: tempo ( ---)
   6000 for next ;
  
: int-action ( ---)
    di  \ disable interrupt
    tempo
    LED.toggle
    tempo
    ei
  ;i

: int-enable ( ---)
    ['] int-action INT0Addr int!
    ei                  \ ei = Enable Interrupt
  ;

init.ports
init.interrupt
int-enable

